import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.0' // Shadow plugin for uber JARs
}

group = 'sdle'

repositories {
    mavenCentral()
}

dependencies {
    // Dependencies for both client and server
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation 'org.zeromq:jeromq:0.5.4'
    implementation 'org.xerial:sqlite-jdbc:3.36.0'
    implementation 'com.google.code.gson:gson:2.8.8'
}

def clientMainClassName = 'sdle.client.Client'
def serverMainClassName = 'sdle.server.Server'
def routerMainClassName = 'sdle.router.Router'

application {
    // Set the main class for the application (not necessary for shadowJar task)
    mainClassName = clientMainClassName
}

// Configuration for client JAR
tasks.register('clientJar', ShadowJar) {
    archiveBaseName = 'client'
    manifest {
        attributes(
                'Main-Class': clientMainClassName
        )
    }
    from sourceSets.main.output
    configurations = [project.configurations.runtimeClasspath]
}

// Configuration for server JAR
tasks.register('serverJar', ShadowJar) {
    archiveBaseName = 'server'
    manifest {
        attributes(
                'Main-Class': serverMainClassName
        )
    }
    from sourceSets.main.output
    configurations = [project.configurations.runtimeClasspath]
}

// Configuration for router JAR
tasks.register('routerJar', ShadowJar) {
    archiveBaseName = 'router'
    manifest {
        attributes(
                'Main-Class': routerMainClassName
        )
    }
    from sourceSets.main.output
    configurations = [project.configurations.runtimeClasspath]
}

tasks.test {
    useJUnitPlatform()
}

// Define dependencies specific to each JAR
configurations {
    clientImplementation.extendsFrom implementation
    serverImplementation.extendsFrom implementation
    routerImplementation.extendsFrom implementation
}

dependencies {
    // Dependencies specific to the client
    clientImplementation 'org.xerial:sqlite-jdbc:3.36.0'
}
